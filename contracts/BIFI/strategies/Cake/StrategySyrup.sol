// SPDX-License-Identifier: MIT

pragma solidity ^0.6.0;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/token/ERC20/SafeERC20.sol";
import "@openzeppelin/contracts/math/SafeMath.sol";
import "@openzeppelin/contracts/utils/Address.sol";
import "@openzeppelin/contracts/utils/Pausable.sol";

import "../../interfaces/common/IUniswapRouterETH.sol";
import "../../interfaces/pancake/ISmartChef.sol";

/**
 * @title Strategy Syrup
 * @author sirbeefalot & superbeefyboy
 * @dev Implementation of a strategy to get yields from farming a Syrup pool at Pancakeswap.
 *
 * The strategy simply deposits whatever funds it receives from the vault into the configured pool (SmartChef). 
 * Rewards generated by the SmartChef can be harvested, swapped for more CAKE, and deposited again for compound farming.
 * This strat is currently compatible with all Syrup pools.
 */

contract StrategySyrup is Ownable, Pausable {
    using SafeERC20 for IERC20;
    using Address for address;
    using SafeMath for uint256;

    /**
     * @dev Tokens Used:
     * {wbnb} - Required for liquidity routing when doing swaps.
     * {cake} - Token that the strategy maximizes. The same token that users deposit in the vault.
     * {bifi} - BeefyFinance token, used to send funds to the treasury.
     * {output} - Token generated by staking CAKE.
     */
    address constant public wbnb  = address(0xbb4CdB9CBd36B01bD1cBaEBF2De08d9173bc095c);
    address constant public cake  = address(0x0E09FaBB73Bd3Ade0a17ECC321fD13a19e81cE82);
    address constant public bifi  = address(0xCa3F508B8e4Dd382eE878A314789373D80A5190A);
    address public output;

    /**
     * @dev Third Party Contracts:
     * {unirouter} - PancakeSwap unirouter
     * {smartchef} - SmartChef contract. Stake CAKE, get {output} token.
     */
    address constant public unirouter = address(0x05fF2B0DB69458A0750badebc4f9e13aDd608C7F);
    address public smartchef;

    /**
     * @dev Beefy Contracts:
     * {rewards} - Reward pool where the strategy fee earnings will go.
     * {treasury} - Address of the BeefyFinance treasury
     * {vault} - Address of the vault that controls the strategy's funds.
     */
    address constant public rewards = address(0x453D4Ba9a2D594314DF88564248497F7D74d6b2C);
    address constant public treasury = address(0x4A32De8c248533C28904b24B4cFCFE18E9F2ad01);
    address public vault;

    /**
     * @dev Distribution of fees earned. This allocations relative to the % implemented on chargeFees().
     * Current implementation separates 6% for fees.
     *
     * {REWARDS_FEE} - 4% goes to BIFI holders through the {rewards} pool.
     * {CALL_FEE} - 0.5% goes to whoever executes the harvest function as gas subsidy.
     * {TREASURY_FEE} - 1.5% goes to the treasury.
     * {MAX_FEE} - Aux const used to safely calc the correct amounts.
     *
     * {WITHDRAWAL_FEE} - Fee taxed when a user withdraws funds. 10 === 0.1% fee.
     * {WITHDRAWAL_MAX} - Aux const used to safely calc the correct amounts.
     */
    uint constant public REWARDS_FEE = 667;
    uint constant public CALL_FEE = 83;
    uint constant public TREASURY_FEE = 250;
    uint constant public MAX_FEE = 1000;

    uint constant public WITHDRAWAL_FEE = 10;
    uint constant public WITHDRAWAL_MAX = 10000;

    /**
     * @dev Routes we take to swap tokens using PancakeSwap.
     * {outputToCakeRoute} - Route we take to get from {output} into {cake}.
     * {outputToWbnbRoute} - Route we take to get from {output} into {wbnb}.
     * {wbnbToBifiRoute} - Route we take to get from {wbnb} into {bifi}.
     */
    address[] public outputToCakeRoute;
    address[] public outputToWbnbRoute;
    address[] public wbnbToBifiRoute = [wbnb, bifi];

    /**
     * @dev Initializes the strategy
     * @param _smartchef The SmartChef to use
     * @param _vault Address of the parent vault
     */
    constructor(address _smartchef, address _vault) public {
        smartchef = _smartchef;
        vault = _vault;
        output = ISmartChef(smartchef).rewardToken();

        outputToCakeRoute = [output, wbnb, cake];
        outputToWbnbRoute = [output, wbnb];

        IERC20(output).safeApprove(unirouter, uint(-1));
        IERC20(wbnb).safeApprove(unirouter, uint(-1));
        IERC20(cake).safeApprove(smartchef, uint(-1));
    }

    /**
     * @dev Function that puts the funds to work.
     * It gets called whenever someone deposits in the strategy's vault contract.
     * It deposits {cake} in the SmartChef to farm {output}.
     */
    function deposit() public whenNotPaused {
        uint256 cakeBal = IERC20(cake).balanceOf(address(this));

        if (cakeBal > 0) {
            ISmartChef(smartchef).deposit(cakeBal);
        }
    }

    /**
     * @dev Withdraws {cake} from the SmartChef and returns it to the vault.
     * @param _amount How much {cake} to withdraw
     */
    function withdraw(uint256 _amount) external {
        require(msg.sender == vault, "!vault");

        uint256 cakeBal = IERC20(cake).balanceOf(address(this));

        if (cakeBal < _amount) {   
            ISmartChef(smartchef).withdraw(_amount.sub(cakeBal));
            cakeBal = IERC20(cake).balanceOf(address(this));
        }

        if (cakeBal > _amount) {
            cakeBal = _amount;    
        }
        
        if (tx.origin == owner()) {
            IERC20(cake).safeTransfer(vault, cakeBal);
        } else {
            uint256 _fee = cakeBal.mul(WITHDRAWAL_FEE).div(WITHDRAWAL_MAX); 
            IERC20(cake).safeTransfer(vault, cakeBal.sub(_fee));
        }        
    }

    /**
     * @dev Core function of the strat, in charge of collecting and re-investing rewards.
     * 1. It claims rewards from the SmartChef
     * 2. It swaps the {output} token for {cake}
     * 3. It charges the system fee and sends it to BIFI stakers.
     * 4. It re-invests the remaining profits.
     */
    function harvest() public whenNotPaused {
        require(!Address.isContract(msg.sender), "!contract");
        ISmartChef(smartchef).deposit(0);
        chargeFees();
        swapRewards();
        deposit();
    }

    /**
     * @dev Takes out 6% as system fees from the rewards. 
     * 0.5% -> Call Fee
     * 1.5% -> Treasury fee
     * 4% -> BIFI Holders
     */
    function chargeFees() internal {
        uint256 toWbnb = IERC20(output).balanceOf(address(this)).mul(6).div(100);
        IUniswapRouterETH(unirouter).swapExactTokensForTokens(toWbnb, 0, outputToWbnbRoute, address(this), now.add(600));
    
        uint256 wbnbBal = IERC20(wbnb).balanceOf(address(this));

        uint256 callFee = wbnbBal.mul(CALL_FEE).div(MAX_FEE);
        IERC20(wbnb).safeTransfer(tx.origin, callFee);
        
        uint256 treasuryHalf = wbnbBal.mul(TREASURY_FEE).div(MAX_FEE).div(2);
        IERC20(wbnb).safeTransfer(treasury, treasuryHalf);
        IUniswapRouterETH(unirouter).swapExactTokensForTokens(treasuryHalf, 0, wbnbToBifiRoute, treasury, now.add(600));
        
        uint256 rewardsFee = wbnbBal.mul(REWARDS_FEE).div(MAX_FEE);
        IERC20(wbnb).safeTransfer(rewards, rewardsFee);
    }

    /**
     * @dev Swaps whatever {output} it has for more cake.
     */
    function swapRewards() internal {
        uint256 outputBal = IERC20(output).balanceOf(address(this));
        IUniswapRouterETH(unirouter).swapExactTokensForTokens(outputBal, 0, outputToCakeRoute, address(this), now.add(600));
    }

    /**
     * @dev Function to calculate the total underlaying {cake} held by the strat.
     * It takes into account both funds at hand and funds allocated in the SmartChef.
     */
    function balanceOf() public view returns (uint256) {
        return balanceOfCake().add(balanceOfPool());
    }

    /**
     * @dev It calculates how much {cake} the contract holds.
     */
    function balanceOfCake() public view returns (uint256) {
        return IERC20(cake).balanceOf(address(this));
    }

    /**
     * @dev It calculates how much {cake} the strategy has allocated in the SmartChef
     */
    function balanceOfPool() public view returns (uint256) {
        (uint256 _amount, ) = ISmartChef(smartchef).userInfo(address(this));
        return _amount;
    }

    /**
     * @dev Function that has to be called as part of strat migration. It sends all the available funds back to the 
     * vault, ready to be migrated to the new strat.
     */ 
    function retireStrat() external {
        require(msg.sender == vault, "!vault");

        ISmartChef(smartchef).emergencyWithdraw();

        uint256 cakeBal = IERC20(cake).balanceOf(address(this));
        IERC20(cake).transfer(vault, cakeBal);
    }

    /**
     * @dev Withdraws all funds from the SmartChef, leaving rewards behind.
     */
    function panic() external onlyOwner {
        pause();
        ISmartChef(smartchef).emergencyWithdraw();
    }

    /**
     * @dev Pauses the strat.
     */
    function pause() public onlyOwner {
        _pause();

        IERC20(output).safeApprove(unirouter, 0);
        IERC20(wbnb).safeApprove(unirouter, 0);
        IERC20(cake).safeApprove(smartchef, 0);
    }

    /**
     * @dev Unpauses the strat.
     */
    function unpause() external onlyOwner {
        _unpause();

        IERC20(output).safeApprove(unirouter, uint(-1));
        IERC20(wbnb).safeApprove(unirouter, uint(-1));
        IERC20(cake).safeApprove(smartchef, uint(-1));
    }
}